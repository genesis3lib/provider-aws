terraform {
  required_version = ">= 1.5"
  required_providers {
    aws = {
      source  = "hashicorp/aws"
      version = "~> 5.0"
    }
  }
}

provider "aws" {
  region = var.aws_region

  default_tags {
    tags = {
      Project   = "{{projectName}}"
      Purpose   = "terraform-state-management"
      ManagedBy = "terraform"
    }
  }
}

# Get current AWS account ID to ensure unique S3 bucket names
data "aws_caller_identity" "current" {}

variable "aws_region" {
  description = "AWS region for Terraform state resources"
  type        = string
  default     = "{{region}}"
}

variable "state_bucket_name" {
  description = "Name for the Terraform state S3 bucket (account ID will be appended)"
  type        = string
  default     = "{{projectName}}-tfstate"
}

locals {
  # Append AWS account ID to bucket name for global uniqueness
  full_bucket_name = "${var.state_bucket_name}-${data.aws_caller_identity.current.account_id}"
}

variable "environment" {
  description = "Environment name (production, test, or any branch name)"
  type        = string
  validation {
    condition     = can(regex("^[a-zA-Z0-9-_]+$", var.environment))
    error_message = "Environment must contain only alphanumeric characters, hyphens, and underscores."
  }
}

variable "lock_table_name" {
  description = "Base name for the DynamoDB table for Terraform locks"
  type        = string
  default     = "{{projectName}}-terraform-locks"
}

resource "aws_s3_bucket" "terraform_state" {
  bucket = local.full_bucket_name

  lifecycle {
    prevent_destroy = true
  }

  tags = {
    Name        = "Terraform-State-Bucket"
    Description = "Terraform state ${var.environment}"
    Environment = var.environment
  }
}

resource "aws_s3_bucket_versioning" "terraform_state" {
  bucket = aws_s3_bucket.terraform_state.id
  versioning_configuration {
    status = "Enabled"
  }
}

resource "aws_s3_bucket_server_side_encryption_configuration" "terraform_state" {
  bucket = aws_s3_bucket.terraform_state.id

  rule {
    apply_server_side_encryption_by_default {
      sse_algorithm = "AES256"
    }
  }
}

resource "aws_s3_bucket_public_access_block" "terraform_state" {
  bucket = aws_s3_bucket.terraform_state.id

  block_public_acls       = true
  block_public_policy     = true
  ignore_public_acls      = true
  restrict_public_buckets = true
}

resource "aws_dynamodb_table" "terraform_locks" {
  name         = "${var.lock_table_name}-${var.environment}"
  billing_mode = "PAY_PER_REQUEST"
  hash_key     = "LockID"

  attribute {
    name = "LockID"
    type = "S"
  }

  tags = {
    Name        = "Terraform-Lock-Table-${var.environment}"
    Description = "Terraform lock ${var.environment}"
    Environment = var.environment
  }
}

output "state_bucket_name" {
  description = "Name of the S3 bucket for Terraform state"
  value       = aws_s3_bucket.terraform_state.bucket
}

output "lock_table_name" {
  description = "Name of the DynamoDB table for Terraform locks"
  value       = aws_dynamodb_table.terraform_locks.name
}

output "aws_region" {
  description = "AWS region where resources were created"
  value       = var.aws_region
}